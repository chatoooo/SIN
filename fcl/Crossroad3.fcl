// Block definition (there may be more than one block per file)
FUNCTION_BLOCK Crossroad

// Definice vstupu - procentualni obsazeni indukcnich smycek
VAR_INPUT				
	zidToMal : REAL;	// Zidenice -> Malomerice
	malToZid : REAL;	// Malomerice -> Zidenice 
	vinToZid : REAL;	// Vinohrady -> Zidenice
	malToVin : REAL;	// Malomerice -> odbocka Vinohrady
END_VAR

// Definice vystupnich hodnot
VAR_OUTPUT
    gZidToMal : REAL;  // doba zelene na semaforu Zidenice -> Malomerice
    gMalToZid : REAL;  // doba zelene na semaforu Malomerice -> Zidenice
    gVinToZid : REAL;  // doba zelene na semaforu Vinohrady -> Zidenice
    gMalToVin : REAL;  // doba zelene na semaforu Malomerice -> odbocka Vinohrady
END_VAR

// fuzzyfikace
FUZZIFY zidToMal
    TERM short := (0, 1) (12, 1) (20, 0); 
    TERM medium := (12, 0) (20, 1) (30, 1) (40, 0);
    TERM long := (30, 0) (40, 1) (100, 1);
END_FUZZIFY

FUZZIFY malToZid
    TERM short := (0, 1) (12, 1) (20, 0); 
    TERM medium := (12, 0) (20, 1) (30, 1) (40, 0);
    TERM long := (30, 0) (40, 1) (100, 1);
END_FUZZIFY

FUZZIFY vinToZid
    TERM short := (0, 1) (12, 1) (20, 0); 
    TERM medium := (12, 0) (20, 1) (30, 1) (40, 0);
    TERM long := (30, 0) (40, 1) (100, 1);
END_FUZZIFY

FUZZIFY malToVin
    TERM short := (0, 1) (12, 1) (20, 0); 
    TERM medium := (12, 0) (20, 1) (30, 1) (40, 0);
    TERM long := (30, 0) (40, 1) (100, 1);
END_FUZZIFY

// defuzzyfikace
DEFUZZIFY gZidToMal
    TERM gShort := (5,0) (10,1) (20,0);
	TERM gMedium := (15,0) (25,1) (35,0);
	TERM gLong := (30,0) (40,1) (50,0);
	METHOD : COG;
	DEFAULT := 15;
END_DEFUZZIFY

DEFUZZIFY gMalToZid
    TERM gShort := (5,0) (10,1) (20,0);
	TERM gMedium := (15,0) (25,1) (35,0);
	TERM gLong := (30,0) (40,1) (50,0);
	METHOD : COG;
	DEFAULT := 15;
END_DEFUZZIFY

DEFUZZIFY gVinToZid
    TERM gShort := (5,0) (10,1) (20,0);
	TERM gMedium := (15,0) (25,1) (35,0);
	TERM gLong := (30,0) (40,1) (50,0);
	METHOD : COA;
	DEFAULT := 15;
END_DEFUZZIFY

DEFUZZIFY gMalToVin
    TERM gShort := (5,0) (8,1) (15,0);
	TERM gMedium := (11,0) (16,1) (21,0);
	TERM gLong := (17,0) (23,1) (30,0);
	METHOD : COG;
	DEFAULT := 10;
END_DEFUZZIFY


// blok pravidel
RULEBLOCK Rule1
	AND : MIN;	// and
	ACT : MIN;	// aktivacni metoda
	ACCU : MAX;	// akumulacni metody

	// smer Vinohrandy -> Zidenice
	RULE 1 : IF vinToZid IS short THEN gVinToZid IS gShort;
	RULE 2 : IF vinToZid IS medium THEN gVinToZid IS gMedium;
	RULE 3 : IF vinToZid IS long THEN gVinToZid IS gLong;
	
	// smer Zidenice -> Malomerice
	RULE 4 : IF zidToMal IS short THEN gZidToMal IS gShort;
	RULE 5 : IF zidToMal IS medium THEN gZidToMal IS gMedium;
	RULE 6 : IF zidToMal IS long THEN gZidToMal IS gLong;
	
	// smer Malomerice -> Zidenice
	RULE 7 : IF zidToMal IS short AND malToZid IS short THEN gMalToZid IS gShort;
	RULE 8 : IF zidToMal IS short AND malToZid IS medium THEN gMalToZid IS gMedium;
	RULE 9 : IF zidToMal IS short AND malToZid IS long THEN gMalToZid IS gLong;
	RULE 10 : IF zidToMal IS medium AND malToZid IS short OR malToZid IS medium THEN gMalToZid IS gMedium;
	RULE 11 : IF zidToMal IS medium AND malToZid IS long THEN gMalToZid IS gLong;
	RULE 12 : IF zidToMal IS long THEN gMalToZid IS gLong;
	
	// Malomerice -> odbocka Vinohrady
	RULE 13 : IF malToVin IS short THEN gMalToVin IS gShort;
	RULE 14 : IF malToVin IS medium THEN gMalToVin IS gMedium;
	RULE 15 : IF malToVin IS long THEN gMalToVin IS gLong;
	
	
	RULE 16 : IF vinToZid IS long AND zidToMal IS long AND malToZid IS long AND malToVin IS long THEN gVinToZid IS gMedium;
	RULE 17 : IF vinToZid IS long AND zidToMal IS long AND malToZid IS long AND malToVin IS long THEN gZidToMal IS gMedium;
	RULE 18 : IF vinToZid IS long AND zidToMal IS long AND malToZid IS long AND malToVin IS long THEN gMalToZid IS gMedium;
	RULE 19 : IF vinToZid IS long AND zidToMal IS long AND malToZid IS long AND malToVin IS long THEN gMalToZid IS gMedium;
END_RULEBLOCK	
	
END_FUNCTION_BLOCK